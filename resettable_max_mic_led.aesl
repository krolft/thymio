<!DOCTYPE aesl-source>
<network>


<!--list of global events-->


<!--list of constants-->
<constant value="2" name="NUMBER_SECS_VOICE_TO_RESET"/>
<constant value="10" name="RESET_MIC_INTENSITY"/>


<!--show keywords state-->
<keywords flag="true"/>


<!--source code of node thymio-II-->
<node name="thymio-II">
# init
var mic_max = 0
var sound_level = 0
var number_seconds_voice = 0
var reset_ongoing = 0

# turn off sound LED
call leds.sound(0)

# init timer (1 s) to watch for sound LED to be resetted 
timer.period[0] = 1000
# init microphon intensity threshold to enable mic event
mic.threshold = 10

sub set_sound_led
	sound_level = mic_max / 10
    call leds.sound(sound_level)

onevent mic
if mic.intensity > mic_max and reset_ongoing == 0 then
	mic_max = mic.intensity
	callsub set_sound_led
end

onevent timer0
if  mic.intensity > RESET_MIC_INTENSITY then
	number_seconds_voice += 1
else
	number_seconds_voice = 0
	reset_ongoing = 0
end
if number_seconds_voice == NUMBER_SECS_VOICE_TO_RESET then
	mic_max = 0
	reset_ongoing = 1
	callsub set_sound_led
end



</node>


</network>
